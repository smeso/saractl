# Maintainer: Salvatore Mesoraca <s.mesoraca16@gmail.com>
pkgname=saractl
pkgver=0.2
pkgrel=1
pkgdesc="Userspace counterpart of the S.A.R.A. LSM"
arch=('any')
url="https://github.com/smeso/saractl"
license=('GPL3')
depends=('python-setuptools' 'python-pyelftools' 'python-prctl' 'python-pyxattr')
makedepends=('git')
backup=('etc/sara/main.conf'
        'etc/sara/wxprot.conf.d/99_wxprot.conf')
source=("git+https://github.com/smeso/saractl.git#tag=v${pkgver}?signed")
validpgpkeys=('D7286260BBF31719A2759FA485F0580B9DACBE6E' # Salvatore Mesoraca
             )
sha256sums=('SKIP')

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 debian/sara.service "${pkgdir}/usr/lib/systemd/system/sara.service"
}
